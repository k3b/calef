apply plugin: 'com.android.application'

android {
    compileSdk 34
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/groovy/**', 'zoneinfo-global/**']
        }
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', '**/package-info.java', 'META-INF/groovy-release-info.properties', 'META-INF/INDEX.LIST', 'META-INF/groovy/**', 'zoneinfo-global/**', 'org/apache/commons/codec/language/bm/*.txt']
        }
    }


    defaultConfig {
        applicationId "de.k3b.android.calef"
        // Android 4.0 (api14)
        minSdkVersion 14
        //noinspection EditedTargetSdkVersion
        targetSdkVersion 34

        versionCode 6
        versionName "1.1.2.1"

        // 1.1.2.1 (6) maintanance Updated libs and compileSdk
        // 1.1.2 (5) Fixed Initialisation Error
        // 1.1.1 (4) reordered Settings; Fixed Initialisation Error
        // 1.1.0 (3) added optional message prefix ("I have added this to my calendar")
        // 1.0.1 (2) fixed translation issues
        // 1.0.0 (1) initial Release translated in en and de
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "debug_title", ""

        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            resValue "string", "debug_title", "DEBUG-" + getDate()
        }
    }
    compileOptions {
        // java 8 needed for android-4.x
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'de.k3b.android.calef'
    lint {
        abortOnError false
        // checkReleaseBuilds false

        // http://stackoverflow.com/questions/31350350/generating-signed-apk-error7-missingtranslation-in-build-generated-res-gen
        // MissingTranslation : not all crowdwin translations are complete so ignore them
        // ValidFragment : local (dialog-)fragment class for customized directory picker is fragile but on rotation code makes sure that dialog is closed.
        // disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'AndroidLintPluralsCandidate', 'AndroidLintRtlHardcoded'
        // disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'ExpiredTargetSdkVersion'
        disable 'MissingTranslation', 'ContentDescription', 'ExpiredTargetSdkVersion'
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.0'

    // ical4j see https://ical4j.github.io/ical4j-user-guide/examples/
//    implementation 'org.mnode.ical4j:ical4j:3.1.0' // requires api-26 + java11
//    implementation 'org.mnode.ical4j:ical4j:3.0.29' // requires api-26 + java11
//    implementation 'org.mnode.ical4j:ical4j:1.0.8' // most recent version 1.x: compiles with api-7 + java6

    //noinspection GradleDependency
    implementation 'org.mnode.ical4j:ical4j:2.2.7' // most recent version 2.x: compiles with api-14 + java8

    implementation 'backport-util-concurrent:backport-util-concurrent:3.1'

    // do not automatically update to 20041127.091804 which is much older that 1.16.0
    //noinspection GradleDependency
    implementation 'commons-codec:commons-codec:1.16.0'

    // do not automatically update to 20030203.000129 which is much older that 2.6
    //noinspection GradleDependency
    implementation 'commons-lang:commons-lang:2.6'

    testImplementation 'junit:junit:4.13.2'

    implementation 'org.slf4j:slf4j-api:2.0.9'
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'
    // causes warning in unittests: "SLF4J: Class path contains multiple SLF4J bindings."
    implementation 'uk.uuid.slf4j:slf4j-android:2.0.9-0'
    // uses app/src/main/resources/uk/uuid/slf4j/android/config.properties
}


static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HH:mm')
    return formattedDate
}

