apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    packagingOptions {
        // prevent duplicate files
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '**/package-info.java'
        exclude 'META-INF/groovy-release-info.properties'
        exclude 'META-INF/INDEX.LIST'
        exclude('META-INF/groovy/**')       //  124 files (uncompressed 215kb) with unused language specific rules
        exclude('zoneinfo-global/**')       //  ?? nearly a duplicate of zoneinfo 217KB ??

        exclude('org/apache/commons/codec/language/bm/*.txt')       //  124 files (uncompressed 215kb) with unused language specific rules
    }

    defaultConfig {
        applicationId "de.k3b.android.calef"
        // Android 4.0 (api14)
        minSdkVersion 14
        targetSdkVersion 30

        versionCode 3
        versionName "1.1.0"

        // 1.1.0 (3) added optional message prefix ("I have added this to my calendar")
        // 1.0.1 (2) fixed translation issues
        // 1.0.0 (1) initial Release translated in en and de
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "debug_title", ""

        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            resValue "string", "debug_title", "DEBUG-" + getDate()
        }
    }
    lintOptions {
        // http://stackoverflow.com/questions/31350350/generating-signed-apk-error7-missingtranslation-in-build-generated-res-gen
        // MissingTranslation : not all crowdwin translations are complete so ignore them
        // ValidFragment : local (dialog-)fragment class for customized directory picker is fragile but on rotation code makes sure that dialog is closed.
        // disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'AndroidLintPluralsCandidate', 'AndroidLintRtlHardcoded'
        // disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'ExpiredTargetSdkVersion'
        disable 'MissingTranslation','ContentDescription', 'ExpiredTargetSdkVersion'

        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'

    // ical4j see https://ical4j.github.io/ical4j-user-guide/examples/
//    implementation 'org.mnode.ical4j:ical4j:3.1.0' // requires api-26
//    implementation 'org.mnode.ical4j:ical4j:3.0.29' // requires api-26
//    implementation 'org.mnode.ical4j:ical4j:1.0.8' // most recent version 1.x: compiles with api-7 + java6

    //noinspection GradleDependency
    implementation 'org.mnode.ical4j:ical4j:2.2.6' // most recent version 2.x: compiles with api-14 + java8

    implementation 'backport-util-concurrent:backport-util-concurrent:3.1'
    implementation 'commons-codec:commons-codec:1.11'
    implementation 'commons-lang:commons-lang:2.6'
    testImplementation 'junit:junit:4.13.2'

    implementation 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'org.slf4j:slf4j-simple:1.7.32'    // causes warning in unittests: "SLF4J: Class path contains multiple SLF4J bindings."
    implementation 'uk.uuid.slf4j:slf4j-android:1.7.32-0' // uses app/src/main/resources/uk/uuid/slf4j/android/config.properties
}


static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HH:mm')
    return formattedDate
}

